// Build instructions for Android library.

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.10.+'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.10.+'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'android-library'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        versionCode = VERSION_CODE
        versionName = VERSION_NAME
        packageName "io.keen"
    }

    sourceSets {
        main.setRoot('src/main')
        androidTest.setRoot('src/test')
    }
}

// Set the source and target to Java 6.
tasks.withType(JavaCompile) {
    options.compilerArgs << "-source" << "1.6" << "-target" << "1.6"
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name

    // For each variant add a task to copy the core classes so they are included in the AAR.
    def copyTask = project.tasks.create(name: "copyCoreToAndroid${name.capitalize()}", type: Copy) {
        from(project(':core').sourceSets.main.output)
        into(variant.javaCompile.destinationDir)
    }
    copyTask.dependsOn variant.javaCompile
    variant.packageLibrary.dependsOn copyTask

    // Also define a task to create a raw JAR to support non-AAR users.
    def rawJarTask = project.tasks.create(name: "rawAndroidJar${name.capitalize()}", type: Jar) {
        archiveName="keen-client-api-android-${project.version}.jar"
        from(variant.javaCompile.destinationDir)
    }
    rawJarTask.dependsOn copyTask
}

dependencies {
    compile project(':core')
}

archivesBaseName = "keen-client-api-android"

apply from: file("${parent.rootDir}/gradle/gradle-mvn-publish-android.gradle")
