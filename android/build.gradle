// Adapted from the Android Gradle plugin code.
private String getAndroidSdkDir() {
    def ant = project.ant
    def sdkDir

    ant.property(file: 'local.properties')

    // Determine the sdkDir value.
    // First, let's try the sdk.dir property in local.properties file.
    try {
        sdkDir = ant['sdk.dir']
    } catch (MissingPropertyException e) {
        sdkDir = null
    }

    if (sdkDir == null || sdkDir.length() == 0) {
        // No local.properties and/or no sdk.dir property: let's try ANDROID_HOME
        sdkDir = System.getenv("ANDROID_HOME")
        // Propagate it to the Gradle's Ant environment
        if (sdkDir != null) {
            ant.setProperty("sdk.dir", sdkDir)
        }
    }

    // Check for sdkDir correctly valued, and in case throw an error
    if (sdkDir == null || sdkDir.length() == 0) {
        throw new MissingPropertyException("Unable to find location of Android SDK. Please read documentation.")
    }

    return sdkDir
}

def sdkDir = getAndroidSdkDir()
// TODO: Handle Android SDK version robustly.
def androidJar = new File(sdkDir, '/platforms/android-19/android.jar')

dependencies {
    compile files(androidJar)
}

jar {
    from {
        project(':core').configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}

archivesBaseName = "keen-client-api-android"
